// This semantic patch is for module init/exit boiler plate code.
// Here for example module_platform_driver transformation is shown but one can go for any other such transformation
// by doing necessary changes.
// More about this can be found at https://vthakkar1994.wordpress.com/2015/07/09/112/

@r@
declarer name module_init;
identifier f;
@@ 
module_init(f);
 
@s@
declarer name module_exit;
identifier e;
@@ 
module_exit(e);
 
@a@
identifier r.f;
identifier x;
@@ 
static f(...) {return platform_driver_register(&x); }
 
@b depends on a@
identifier s.e,a.x;
@@ 
static e(...) { platform_driver_unregister(&x); }
 
@t depends on r && a@
identifier r.f;
@@
-module_init(f);
 
@v depends on s && a && b@
declarer name module_platform_driver;
identifier s.e, a.x;
@@
-module_exit(e);
+module_platform_driver(x);

@c depends on b@
identifier r.f, a.x;
@@
-static f(...) { return platform_driver_register(&x); }

@d depends on c@
identifier s.e, a.x;
@@

-static e(...) { platform_driver_unregister(&x); }
